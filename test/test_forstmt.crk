# Copyright 2010 Google Inc.
# tests of the "for" statement

sum := 0;

for (int i = 0; i < 10; ++i)
    sum += i;
if (sum != 45)
    puts('Failed on normal "for"');
sum = 0;

for (int i = 0; i < 10; ++i) {
    if (i % 2)
        continue;
    sum += i;
}
if (sum != 20)
    puts('Failed on continuing "for"');

for (;;) {
    sum = 100;
    break;
}
if (sum != 100)
    puts('Failed on empty "for"');

class RangeIter {
    int i, max;
    
    oper init(int start, int max0) : i = start, max = max0 {}

    int elem() { return i; }
    
    bool next() { ++i; return i < max; }
    
    bool toBool() { return i < max; }
}

class Range {
    int start, max;
    oper init(int start0, int max0) : start = start0, max = max0 {}
    RangeIter iter() { return RangeIter(start, max); }
}   

sum = 0;
for (i :in Range(0, 10))
    sum += i;
if (sum != 45)
    puts('Failed iteration with element var def');

if (1) {
    sum = 0;
    int i;
    for (i in Range(0, 10))
        sum += i;
    if (sum != 45)
        puts('Failed iteration over existing variable');
}

sum = 0;
for (i :on Range(0, 10))
    sum += i.elem();
if (sum != 45)
    puts('Failed iteration with iterator var def');

if (1) {
    sum = 0;
    RangeIter i = null;
    for (i on Range(0, 10))
        sum += i.elem();
    if (sum != 45)
        puts('Failed iteration with existing iterator');
}

puts('ok');
