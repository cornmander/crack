
import crack.lang AppendBuffer;
import crack.io Formatter, Writer;
import crack.cont.array Array;

class StringArray : Array[String] {
    oper init(uint size) : Array[String](size) {}
    oper init() {}    
    
    void writeTo(Writer out) {
        out.write('[');
        bool first = true;
        for (elem :in this) {
            if (!first)
                out.write(', ');
            else
                first = false;
            out.write(elem.getRepr());
        }
        out.write(']');
    }

    ## Returns a primitive array based on the array (useful for communicating 
    ## to C bindings)
    ## @unsafe
    array[byteptr] makePrimArray() {
        size := count();
        prim := array[byteptr](size);
        for (int i; i < size; ++i)
            prim[i] = this[i].buffer;
        return prim;
    }

    uint __countBytes() {
        uint total;
        for (elem :in this)
            total += elem.size;
        return total;
    }
    
    ## Join all of the elements into a single string with elements separated 
    ## by 'sep'
    String join(String sep) {
        
        # deal with the empty case
        size := count();
        if (!size)
            return '';
        
        # figure out how much space we need
        total := __countBytes() + sep.size * (size - 1);
        
        AppendBuffer buf = {total};
        first := true;
        for (elem :in this) {
        
            # add the separator for everything but the first string.
            if (first)
                first = false;
            else
                buf.extend(sep);

            buf.extend(elem);
        }
        
        return String(buf, true);
    }
        
}

## Split the string into an array of words delimited by a sequence of 
## ascii whitespace characters.
StringArray split(String val) {

    # rule out the simple case
    if (val.size == 0)
        return StringArray![''];

    # set to true if we are currently in a block of non-whitespace characters
    bool inWord = true;
    
    # figure out how big we need to make the array
    uint count = 1;
    for (uint i = 0; i < val.size; ++i) {
        cur := val[i];
        if (cur == b' ' || cur == b'\t' || cur == b'\n' || cur == b'\r') {
            inWord = !inWord;
            if (inWord)
                ++count;
        }
    }
    if (!inWord) ++count;

    result := StringArray(count);
    
    # do it again, this time create substrings
    int start = 0;
    inWord = true;
    for (int i = 0; i < val.size; ++i) {
        cur := val[i];
        gotSpace := cur == b' ' || cur == b'\t' || cur == b'\n' || 
                    cur == b'\r';
        if (gotSpace && inWord) {
            result.append(val.slice(start, i));
            inWord = false;
        } else if (!gotSpace && !inWord) {
            # first non-space character after a block of whitespace, reset start
            start = i;
            inWord = true;
        }
    }

    # add the last word
    if (inWord)
        result.append(val.slice(start));
    else
        result.append('');

    return result;
}
