// A test suite for crack
// Copyright 2010 Shannon Weyrick <weyrick@mozek.us>

import crack.io cout, cerr, Formatter, Writer, StringWriter;
import crack.sys argv, exit;
import crack.container List, Iterator, TreeMap, KeyVal;
import crack.exp.dir Directory;
import crack.exp.file File, FileInfo;
import crack.exp.regex Regex;

SCREEN_VERSION := ".2";

class Test {

    FileInfo _fInfo = null;
    String _desc = null;
    TreeMap _testData;
    
    bool _isValid = false;
    String _isSkipped = null;

    oper init(): _desc("Unknown") { }

    void loadTemplate() {
        
        f := File(_fInfo.name, "r");
        if (!f.isValid()) {
            cout `file: $(_fInfo.name) could not be opened!\n`;
            _isValid = false;
            return;
        }
        
        // parse out sections
        String lineBuf, curSection;
        StringWriter dataBuf;
        headerPat := Regex('^--(\\w+)--$');
        while (lineBuf = f.nextLine()) {
            //cerr `debug: $buf`;
            // look for section change
            m := headerPat.search(lineBuf);
            if (m) {
                if (!curSection.isEmpty())
                    _testData[curSection] = dataBuf.createString();
                curSection = m.group(1);
                //cerr `debug: switching to group $curSection\n`;
            }
            else {
                // add line to current section
                dataBuf.write(lineBuf);
                if (curSection == 'TEST')
                    _desc = String(lineBuf, 0, lineBuf.size-1);
            }
        }
        
    }

    /// ensure test has required sections and that they contain valid data
    /// also check to see if we're skipping this test for some reason
    void checkTemplate() {        
        // XXX 09/10 - introduces bad IR!
        //if (!_testData["FILE"]) {
        //    _isSkipped = "the template was missing a FILE section";
        //}
        _isValid = true;
    }

    void run() {
        
        if (_isSkipped) {
            cout `$_isSkipped\n`;
            return;
        }

        cout `running [$_fInfo]\n`;

    }

    oper init(FileInfo info): _fInfo = info,
                              _desc("Unknown") {
        loadTemplate();
        checkTemplate();
    }

    void _dump(Writer out) {
        ti := _testData.iter();
        while (ti.nx()) {
            section := String.cast(KeyVal.cast(ti.elem()).key);
            data := String.cast(KeyVal.cast(ti.elem()).val);
            Formatter(out) `[$section]:\n$data`;
        }
    }

    void writeTo(Writer out) {
        Formatter(out) `[$_fInfo]: $_desc\n`;
        //_dump(out);
    }

}

class TestSuite {

    String _rootDir;
    List _testList;
    
    void usage() {
        cout `screen <test dir> <output dir>\n`;
        exit(1);
    }
    
    void scanForTests(Directory dir) {
    
        if (!dir.isValid()) {
            cerr `skipping invalid directory: $dir\n`;
            return;
        }
        
        cout `importing tests from $dir ...\n`;

        Iterator i;
        
        // gather test files
        FileInfo curFile = null;
        i = dir.fileInfoIter();
        while (i.nx()) {
            curFile = FileInfo.cast(i.elem());
            if (curFile.matches("*.crkt")) {
                //cout `found test template file: $curFile\n`;
                test := Test(curFile);
                _testList.append(test);
            }
        }

        // recurse to directories
        Directory nextDir = null;
        i = dir.dirIter();
        while (i.nx()) {
            nextDir = Directory.cast(i.elem());
            scanForTests(nextDir);
        }


    }

    void runTests() {

        //cout `\nrunning tests...\n`;

        Test curTest = null;

        i := _testList.iter();
        while (i.nx()) {
            curTest = Test.cast(i.elem());
            curTest.run();
        }

    }

    oper init() {
        
        if (argv.count() <= 1) {
            usage();            
        }

        cout `screen v$(SCREEN_VERSION): now testing for crack\n`;

        _rootDir = argv[1];
        scanForTests(Directory(_rootDir));

        runTests();
        
    }

}

suite := TestSuite();
